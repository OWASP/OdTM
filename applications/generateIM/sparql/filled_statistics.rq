PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
 
# the first source:
PREFIX bm: <http://www.grsu.by/net/OdTMBaseThreatModel#> 
# another one:
PREFIX : <http://www.grsu.by/net/OdTMIntegratedModel#> 

SELECT ?ATTCKs ?CAPECs ?CWEs ?CVEs ?ATTCKandCAPECs  ?CAPECandCWEs ?CWEandCVEs
FROM <../ttl/OdTMIntegratedModel_filled.ttl>
WHERE
{
  {
    SELECT (count(?a) as ?ATTCKs)
    WHERE { ?a rdf:type :ATTCKTechnique .}
  }

  {
    SELECT (count(?d) as ?CAPECs)
    WHERE { ?d rdf:type :CAPEC .}
  }

  {
    SELECT (count(?g) as ?CWEs)
    WHERE { ?g rdf:type :CWE .}
  }

  {
    SELECT (count(?k) as ?CVEs)
    WHERE { ?k rdf:type :CVE .}
  }



  {
    SELECT (count(?b) as ?ATTCKandCAPECs)
    WHERE { ?b bm:refToCAPEC ?c . }
  }

  {
    SELECT (count(?h) as ?CAPECandCWEs)
    WHERE { ?h bm:refToCWE ?l . }
  }

  {
    SELECT (count(?o) as ?CWEandCVEs)
    WHERE { ?o bm:refToCVE ?p . }
  }

}

